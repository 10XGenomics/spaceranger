#
# Copyright (c) 2019 10X Genomics, Inc. All rights reserved.
#

@include "_basic_sc_rna_counter_stages.mro"
@include "_cr_lib_stages.mro"

pipeline _SLFE_CELLS_REPORTER(
    in  map<ChemistryDef> chemistry_defs,
    in  ReferenceInfo     reference_info,
    in  CellCalling       cell_calling_config,
    in  frf.bincode       slfe_feature_reference,
    in  tps.json          target_panel_summary,
    in  h5                matrices_h5,
    in  map[]             read_chunks,
    in  int               gem_well,
    in  bui[]             report_mol_inputs,
    in  json              matrix_computer_summary,
    in  h5                barcode_summary,
    in  csv               filtered_barcodes,
    in  csv               per_probe_metrics,
    in  json              filter_barcodes_summary,
    in  csv               per_barcode_metrics,
    in  bool              include_introns,
    in  bool              filter_probes,
    in  bool              disable_subsampling,
    in  string            multi_config_sha,
    in  bi.bincode        barcode_index,
    in  string            slide_serial_capture_area,
    out json              summary,
    out h5                molecule_info,
)
{
    call WRITE_MOLECULE_INFO(
        sample_barcodes          = null,
        per_sample_metrics       = null,
        counts_bc_order          = self.report_mol_inputs,
        feature_reference_binary = self.slfe_feature_reference,
        *                        = self,
    )

    call SUBSAMPLE_READS(
        molecule_info = WRITE_MOLECULE_INFO.single_mol_info.h5_file,
        target_mode   = null,
        *             = self,
    ) using (
        disabled = self.disable_subsampling,
        volatile = true,
    )

    call SUMMARIZE_BASIC_REPORTS(
        summary_jsons   = [
            SUBSAMPLE_READS.summary,
            WRITE_MOLECULE_INFO.single_mol_info.summary,
            self.matrix_computer_summary,
            self.filter_barcodes_summary,
        ],
        sample          = null,
        # this is being run "library level", use all bcs
        sample_bcs_only = false,
        recovered_cells = self.cell_calling_config.recovered_cells,
        *               = self,
    )

    return (
        summary       = SUMMARIZE_BASIC_REPORTS.summary,
        molecule_info = WRITE_MOLECULE_INFO.single_mol_info.h5_file,
    )
}

# CELLS_REPORTER but for sliced samples, does not write the molecule info
pipeline _SAMPLE_CELLS_REPORTER(
    in  string           sample,
    in  h5               molecule_info,
    in  CellCallingParam recovered_cells,
    in  h5               matrices_h5,
    in  json             matrix_computer_summary,
    in  csv              filtered_barcodes,
    in  csv              per_barcode_metrics,
    in  h5               barcode_summary,
    in  json             sample_assignment_metrics,
    in  json             count_analyzer_metrics,
    in  json             crispr_analyzer_metrics,
    in  json             targeted_analyzer_metrics,
    in  json             cell_annotation_metrics,
    in  json             cas_metrics,
    in  tps.json         target_panel_summary,
    out json             summary,
)
{
    call SUBSAMPLE_READS(
        molecule_info     = self.molecule_info,
        filtered_barcodes = self.filtered_barcodes,
        target_mode       = null,
    ) using (
        volatile = true,
    )

    call SUMMARIZE_BASIC_REPORTS(
        sample                  = self.sample,
        matrices_h5             = self.matrices_h5,
        filtered_barcodes       = self.filtered_barcodes,
        per_barcode_metrics     = self.per_barcode_metrics,
        matrix_computer_summary = self.matrix_computer_summary,
        barcode_summary         = self.barcode_summary,
        recovered_cells         = self.recovered_cells,
        # we want "all reads" etc to include only those with sample barcodes.
        sample_bcs_only         = true,
        target_panel_summary    = self.target_panel_summary,
        summary_jsons           = [
            self.matrix_computer_summary,
            SUBSAMPLE_READS.summary,
            self.sample_assignment_metrics,
            self.count_analyzer_metrics,
            self.crispr_analyzer_metrics,
            self.targeted_analyzer_metrics,
            self.cell_annotation_metrics,
            self.cas_metrics,
        ],
    )

    return (
        summary = SUMMARIZE_BASIC_REPORTS.summary,
    )
}
